# Git hash
BUILD_ID = "${MEDUSA_METADATA_GIT_HASH}"

# Distro naming
DISTRO = "medusa"
DISTRO_NAME = "Stromer Medusa"
DISTRO_CODENAME = "${MEDUSA_METADATA_CODENAME}"
DISTRO_VERSION = "${MEDUSA_METADATA_GIT_DATE_TIME}-${MEDUSA_METADATA_GIT_HASH_SHORT}${MEDUSA_METADATA_VARIANT}"
OS_RELEASE_FIELDS:append:pn-os-release = " BUILD_ID"
hostname:pn-base-files = "${DISTRO}"

# Image naming
IMAGE_LINK_NAME = "${IMAGE_BASENAME}"
IMAGE_NAME = "${IMAGE_BASENAME}-${MEDUSA_METADATA_CODENAME}-${MEDUSA_METADATA_GIT_DATE_TIME}-${MEDUSA_METADATA_GIT_HASH_SHORT}${MEDUSA_METADATA_VARIANT}${IMAGE_NAME_SUFFIX}"

# SDK naming
SDK_NAME = "${DISTRO}-${SDK_ARCH}"
SDK_VERSION = "${MEDUSA_METADATA_GIT_DATE_TIME}-${MEDUSA_METADATA_GIT_HASH_SHORT}${MEDUSA_METADATA_VARIANT}"
SDKPATHINSTALL = "/opt/${DISTRO}/${SDK_VERSION}"

# Distro features
DISTRO_FEATURES = "fbdev systemd opengl"
DISTRO_FEATURES_BACKFILL_CONSIDERED += "ldconfig"

# Locales
GLIBC_GENERATE_LOCALES = "en_US.UTF-8"

# Machine features
MACHINE_FEATURES:append = " 3g bluetooth can usbhost wifi"

# systemd selected via conf/distro/include/init-manager-${INIT_MANAGER}.inc
INIT_MANAGER = "systemd"

# systemd settings
SYSTEMD_DEFAULT_TARGET = "started.target"

# Remove QML software renderer
RDEPENDS:qtbase:remove = " opengldummy qtdeclarative-render2d"

# Uncomment this to skip medusa image appends.
#BBMASK += "meta-medusa-app/recipes-image/"

# Uncomment this to enable JTAG in kernel.
#SRC_URI:append:pn-linux-mainline = " file://jtag-pm.patch"

# Activate config checker
INHERIT += "sanity"

# Run QA tests on recipes
INHERIT += "recipe_sanity"

# Run QA tests on builds and packages and log them
INHERIT += "insane"
QA_LOG = "1"

# Log information on images and packages
INHERIT += "buildhistory"
BUILDHISTORY_COMMIT = "1"
INHERIT += "buildstats"

# Explicitly define home directory for root
ROOT_HOME = "/home/root"

# Set empty root password and add user
# Remark: The "useradd-staticids" class is not used for simplicity, since all uid's and gid's would have to be defined ourselves.
INHERIT += "extrausers"
EXTRA_USERS_PARAMS = "usermod -p '' root; usermod -s /bin/bash root; \
                      useradd -p '' user; usermod -s /bin/bash user; usermod -G systemd-journal user; \
"

# Mount rootfs as read-only for incremental firmware update
EXTRA_IMAGE_FEATURES = "debug-tweaks read-only-rootfs"

# Do not symlink log to a volatile storage in order to have a persistent systemd journal
VOLATILE_LOG_DIR = "no"

# Source mirror settings
SOURCE_MIRROR_URL = "https://gitlab-yocto-mirror.stromerbike.com/downloads/"
INHERIT += "own-mirrors"

# State mirror settings
BB_HASHSERVE = ""
BB_SIGNATURE_HANDLER = "OEBasicHash"
SSTATE_MIRRORS ?= "file://.* https://gitlab-yocto-mirror.stromerbike.com/sstate-cache/PATH;downloadfilename=PATH"

# Missing dependency in manifest for zipfile https://github.com/python/cpython/blob/3.10/Lib/importlib/readers.py#L2
IMAGE_INSTALL:append = " python3-compression"

# Multiple providers note being shown for util-linux-sfdisk-native
PREFERRED_RPROVIDER_util-linux-sfdisk-native = "util-linux-native"

# Optimize bigger packages used interactively for debugging concerning size
FULL_OPTIMIZATION:append:pn-gdb = " -Os"
FULL_OPTIMIZATION:append:pn-lnav = " -Os"

# Optimize medium packages used interactively for debugging concerning size
FULL_OPTIMIZATION:append:pn-btop = " -Os"
FULL_OPTIMIZATION:append:pn-htop = " -Os"
FULL_OPTIMIZATION:append:pn-ltrace = " -Os"
FULL_OPTIMIZATION:append:pn-nano = " -Os"
FULL_OPTIMIZATION:append:pn-socat = " -Os"
FULL_OPTIMIZATION:append:pn-strace = " -Os"
FULL_OPTIMIZATION:append:pn-sudo = " -Os"
FULL_OPTIMIZATION:append:pn-tcf-agent = " -Os"
FULL_OPTIMIZATION:append:pn-tcpdump = " -Os"
FULL_OPTIMIZATION:append:pn-tmux = " -Os"
FULL_OPTIMIZATION:append:pn-x11vnc = " -Os"
